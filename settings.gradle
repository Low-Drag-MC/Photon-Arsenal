pluginManagement {
    repositories {
        maven { url = "https://maven.fabricmc.net/" }
        maven { url = "https://maven.architectury.dev/" }
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = "https://maven.quiltmc.org/repository/release" }
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    repositories {
         mavenCentral()
    }
    versionCatalogs {
        // Minecraft Version
        def minecraftVersion = "1.20.1"

        // Mod Dependencies Versions

        // Fabric
        def fabricLoaderVersion = "0.14.21" // https://fabricmc.net/develop/
        def fabricApiVersion = "0.83.0" // https://fabricmc.net/develop/
        def sodiumVersion = "mc1.20-0.4.10"
        def irisVersion = "1.6.4+1.20"
        def registrateFabricVersion = "1.3.59-MC1.20"

        // Forge
        def forgeVersion = "47.0.19"
        def rubidiumVersion = "0.6.5"
        def oculusVersion = "1.20-1.6.4"
        def registrateForgeVersion = "MC1.20-1.3.3"

        // Libs
        def quiltMappingsVersion = "5"  // https://lambdaurora.dev/tools/import_quilt.html
        def parchmentVersion = "1.19.3:2023.03.12" // https://parchmentmc.org/docs/getting-started
        def shadowVersion = "7.1.2"
        def architecturyPluginVersion = "3.4-SNAPSHOT"
        def architecturyLoomVersion = "1.0-SNAPSHOT"
        def quiltFlowerVersion = "1.+"
        def macheteVersion = "1.+"
        def ldLibVersion = "1.0.16.b"
        def photonVersion = "1.0.6"
        def geckolibVersion = "4.2"

        fabric {
            def parchment = version("parchment", parchmentVersion)
            library("parchment", "org.parchmentmc.data", "parchment-${minecraftVersion}").versionRef(parchment)

            def fabricLoader = version("fabricLoader", fabricLoaderVersion)
            library("fabricLoader", "net.fabricmc", "fabric-loader").versionRef(fabricLoader)

            def fabricApi = version("fabricApi", "${fabricApiVersion}+${minecraftVersion}")
            library("fabricApi", "net.fabricmc.fabric-api", "fabric-api").versionRef(fabricApi)

            def registrate = version("registrate", registrateFabricVersion)
            library("registrate", "com.tterrag.registrate_fabric", "Registrate").versionRef(registrate)

            def ldLib = version("ldlib", ldLibVersion)
            library("ldlib-common", "com.lowdragmc.ldlib", "ldlib-common-${minecraftVersion}").versionRef(ldLib)
            library("ldlib-fabric", "com.lowdragmc.ldlib", "ldlib-fabric-${minecraftVersion}").versionRef(ldLib)

            def photon = version("photon", photonVersion)
            library("photon-common", "com.lowdragmc.photon", "photon-common-${minecraftVersion}").versionRef(photon)
            library("photon-fabric", "com.lowdragmc.photon", "photon-fabric-${minecraftVersion}").versionRef(photon)

            def geckolib = version("geckolib", geckolibVersion)
            library("geckolib-fabric", "software.bernie.geckolib", "geckolib-fabric-1.20").versionRef(geckolib)

            def sodium = version("sodium", sodiumVersion)
            library("sodium", "maven.modrinth", "sodium").versionRef(sodium)

            def iris = version("iris", irisVersion)
            library("iris", "maven.modrinth", "iris").versionRef(iris)
        }

        forge {
            version("forgeShortVersion", forgeVersion)
            def minecraftForge = version("minecratfForge", "${minecraftVersion}-${forgeVersion}")
            library("minecraftForge", "net.minecraftforge", "forge").versionRef(minecraftForge)

            def ldLib = version("ldlib", ldLibVersion)
            library("ldlib-forge", "com.lowdragmc.ldlib", "ldlib-forge-${minecraftVersion}").versionRef(ldLib)

            def photon = version("photon", photonVersion)
            library("photon-forge", "com.lowdragmc.photon", "photon-forge-${minecraftVersion}").versionRef(photon)

            def registrate = version("registrate", registrateForgeVersion)
            library("registrate", "com.tterrag.registrate", "Registrate").versionRef(registrate)

            def geckolib = version("geckolib", geckolibVersion)
            library("geckolib-forge", "software.bernie.geckolib", "geckolib-forge-1.20").versionRef(geckolib)

            def rubidium = version("rubidium", rubidiumVersion)
            library("rubidium", "maven.modrinth", "rubidium").versionRef(rubidium)

            def oculus = version("oculus", oculusVersion)
            library("oculus", "maven.modrinth", "oculus").versionRef(oculus)
        }

        libs {
            version("quiltMappings", quiltMappingsVersion)
            version("parchment", parchmentVersion)

            def minecraft = version("minecraft", minecraftVersion)
            library("minecraft", "com.mojang", "minecraft").versionRef(minecraft)

            def shadow = version("shadow", shadowVersion)
            plugin("shadow", "com.github.johnrengelman.shadow").versionRef(shadow)

            def architecturyPlugin = version("architectury", architecturyPluginVersion)
            plugin("architectury", "architectury-plugin").versionRef(architecturyPlugin)

            def architecturyLoom = version("architectury-loom", architecturyLoomVersion)
            plugin("architectury-loom", "dev.architectury.loom").versionRef(architecturyLoom)

            def quiltFlower = version("quiltFlower", quiltFlowerVersion)
            plugin("quiltFlower", "io.github.juuxel.loom-quiltflower").versionRef(quiltFlower)

            def machete = version("machete", macheteVersion)
            plugin("machete", "io.github.p03w.machete").versionRef(machete)
        }
    }
}

include("${mod_id}-common")
include("${mod_id}-fabric")
include("${mod_id}-forge")

project(":${mod_id}-common").setProjectDir(new File("common"))
project(":${mod_id}-forge").setProjectDir(new File("forge"))
project(":${mod_id}-fabric").setProjectDir(new File("fabric"))

rootProject.name = "${mod_name}"
